/*
 *  Copyright 2020 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
plugins {
    id 'application'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

application {
    mainClass.set('com.twosigma.beakerx.groovy.kernel.Groovy')
}

ext {
    antlr4Version = "4.5"
}

configurations {
    runtimetools
    antlr
}

configurations.all {
    exclude group: "org.codehaus.groovy", module: "groovy-datetime"
}

dependencies {
    implementation('org.apache.groovy:groovy-datetime')
    implementation "com.twosigma:beakerx-kernel-base:${beakerxVersion}"
    implementation "com.twosigma:beakerx-kernel-base-api:${beakerxVersion}"
    implementation 'org.apache.groovy:groovy-all:4.0.21'

    implementation "org.antlr:antlr4-runtime:${antlr4Version}"

    // these should be transitive from beakerx-kernel-base-api?
//    implementation 'org.apache.commons:commons-lang3:3.6'
//    implementation group:'org.slf4j', name: 'slf4j-log4j12', version: '2.0.13'
//    implementation 'com.google.code.gson:gson:2.8.9'
//    implementation 'commons-beanutils:commons-beanutils:1.9.4'
//    implementation 'io.javalin:javalin:4.4.0'
//    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
//    testImplementation group: 'junit', name: 'junit', version: '4.12'
    //

    antlr "org.antlr:antlr4:${antlr4Version}"

    runtimetools "com.twosigma:beakerx-runtimetools:${beakerxVersion}"

    testImplementation "com.twosigma:beakerx-kernel-base-test:${beakerxVersion}"
}

task antlr4_groovy_lexer(type: JavaExec) {
    mainClass = "org.antlr.v4.Tool"
    workingDir = "src/main/antlr4/"
    args = ["GroovyLexer.g4", "-o", "../java/com/twosigma/beakerx/groovy/autocomplete", "-listener", "-no-visitor", "-package", "com.twosigma.beakerx.groovy.autocomplete", "-encoding", "UTF-8"]
    classpath = configurations.antlr
}

task antlr4_groovy_parser(type: JavaExec) {
    mainClass = 'org.antlr.v4.Tool'
    workingDir = 'src/main/antlr4/'
    args = ['GroovyParser.g4', '-o', '../java/com/twosigma/beakerx/groovy/autocomplete', '-listener', '-no-visitor', '-package', 'com.twosigma.beakerx.groovy.autocomplete', '-encoding', 'UTF-8']
    classpath = configurations.antlr
}

install.dependsOn antlr4_groovy_lexer
install.dependsOn antlr4_groovy_parser

jar {
    manifest {
        attributes("Main-Class": "${application.mainClass}")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.twosigma'
            artifactId 'beakerx-kernel-groovy'
            version "${beakerxVersion}"
            from components.java
        }
    }
}

task copyLibs(type: Copy) {
    dependsOn 'jar'

    from configurations.runtimeClasspath
    from jar
    into "${project.rootDir}/groovy-dist/beakerx_groovy/kernel/lib"
}
install.dependsOn copyLibs

task copyExt(type: Copy) {
    dependsOn 'jar'

    from configurations.runtimetools
    include 'beakerx-runtimetools*.jar'
    into "${project.rootDir}/groovy-dist/beakerx_groovy/kernel/ext"
}
install.dependsOn copyExt

task copyMisc(type: Copy) {
    from("${project.rootDir}/groovy") {
        include '*.png'
        include 'kernel.json'
    }
    into "${project.rootDir}/groovy-dist/beakerx_groovy/kernel"
}
install.dependsOn copyMisc
